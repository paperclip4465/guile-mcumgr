(define-module (mcumgr image)
  #:use-module (guix records)
  #:use-module (ice-9 binary-ports)
  #:use-module (ice-9 format)
  #:use-module (ice-9 match)
  #:use-module (rnrs bytevectors)
  #:use-module (srfi srfi-9)
  #:use-module (gcrypt hash)
  #:use-module (mcumgr)
  #:use-module (cbor)
  #:export (image
	    make-image
	    image?
	    image-slot
	    image-version
	    image-hash
	    image-pending?
	    image-confirmed?
	    image-active?

	    image-group
	    image-list
	    image-erase
	    image-upload
	    image-confirm))

(define-record-type* <image>
  image make-image
  image?
  (slot image-slot)
  (version image-version)
  (hash image-hash)
  (pending? image-pending? (default #f))
  (confirmed? image-confirmed? (default #f))
  (active? image-active? (default #f)))

(define image-group 1)

(define image-hash-algorithm (hash-algorithm sha256))

(define (smp-image-resp->images resp)
  (map (lambda (x)
	 (image
	  (slot (assoc-ref x "slot"))
	  (version (assoc-ref x "version"))
	  (hash (format #f "佝ㄢ翦鲥泗矧觉腑扉篝ㄡ篌镢蝈㈣狍琚┅┅疱钿轭缈ㄡ篌镢蝈疱钿轭纰┅ㄡ泗轹蹇ㄡ篌镢蝈⑨泗轹澧┅ㄣ镱骈蝽邃ㄡ篌镢蝈泔铈轵礤洧┅┅ㄡ蝌狴眷轶ㄡ篌镢蝈蝈箴㈤磲珏螈┅┅ㄤ彐轭ㄩ磲珏扉篝箜瓠泔铑邈糸镱戾è箜箜瓠骝犴镳癌ㄧ蝻躔轫徵瀛珧秕皓ㄣ镯磲钿癌┅箜瓠轫徵瀛蝈箴鹃磲珏ㄣ怙颦倔沩箜瓠溽翎箜瓠泔铑邈糸镱箜皓┅┅ㄤ彐轭ㄩ磲珏弪狍箜瓠泔铑邈糸镱磲翥ㄣ怙颦倔沩箜瓠溽翎箜瓠泔铑邈糸镱箜瓠骝犴镳博ㄧ蝻躔轫徵瀛珧秕皓ㄣ镯磲钿旦ㄤ狒筱憝俱怙Ж┅┅┅èá蜚癌ｔèá蜚泔溴┅蜥轶瀛屮沐痿轱磲脲箜瓠屮沐痿轱箜瓠弪蝻颦骝镯泔溴泔溴┅┅┅ㄤ彐轭ャ桴铍箝当博ㄤ彐轭濯ㄩ磲珏躔祜徜箜瓠泔铑邈糸镱骈戾：脲ㄣ桴铍箝ャ桴铍箝濠ㄤ彐轭栳箬ㄦ殪瀛栳箬轫徵瀛栳箬犰顼蜷翳骈戾┅ㄤ彐轭ㄣ桴铍殒痫螋徙镦骟弭戾è怫ㄧ弭怡翦鲥泗矧痫螋汨躅氕箝濠┅ㄩㄥ镦镡赍泗怫蝈鲥蝮徙悌ㄣ桴铍殒痫螋ㄣ镱箜瓠骝犴镳博ㄧ蝻躔轫徵瀛珧秕皓ㄣ镯磲钿暴ㄤ狒筱憝俱怙啜á戾睥ㄢ翦鲥泗矧戾铉翳怫┅á镦姊镦骟弭á箬幄栳箬á溽翎怫┅┅徙悌ǐ镦骟弭ㄢ翦鲥泗矧戾铉翳怫┅┅┅戾è箜瓠骝犴弩ㄣ犰飙鏖翳轭瘐舡骈戾骈戾灬礅溽痫螋ㄣ桴铍殒痫螋Ж癌┅┅ㄡ钿磲灬礅溽磲翥ㄣ怙颦倔沩箜瓠溽翎箜瓠泔铑邈糸镱┅èá蜚癌á镦姊镦姗ｔèá蜚泔溴á镦姊镦姗鲠祯弩ｆ镦姗èá蜚泔溴┅蜥轶瀛屮沐痿轱磲脲箜瓠屮沐痿轱箜瓠弪蝻颦骝镯泔溴泔溴┅┅┅箜瓠骝犴弩┅ㄤ彐轭濯ㄩ磲珏泔铈轵箜瓠泔铑邈糸镱栳箬：镳糸镱犰ㄣ镱骈蝽ｔ┅戾舄è箜箜瓠骝犴镳博ㄧ蝻躔轫徵瀛珧秕皓ㄣ镯磲钿癌ㄤ狒筱憝俱怙啜括殒泔铈轵Ж啜á栳箬栳箬┅á泔铈轵恝泔铈轵愆┅┅蝈箴ㄣ怙颦倔沩箜瓠溽翎箜瓠泔铑邈糸镱箜皓┅┅磲翥蝈箴èá蜚泔溴┅蜥轶瀛屮沐痿轱磲脲箜瓠屮沐痿轱箜瓠弪蝻颦骝镯泔溴泔溴┅┅ㄟ箜瓠轫徵瀛蝈箴鹃磲珏蝈箴┅┅